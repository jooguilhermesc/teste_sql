USE [corporerm]
GO

/****** Object:  UserDefinedFunction [dbo].[FN_RETORNA_DATA_MOV_MATRICULA_EB]    Script Date: 27/04/2023 19:22:33 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



CREATE FUNCTION [dbo].[FN_RETORNA_DATA_MOV_MATRICULA_EB] (
	@CODCOLIGADA INT, 
	@IDPERLET INT,
	@IDHABILITACAOFILIAL INT,
	@RA VARCHAR (20)
)
/********************************************************************
* AUTOR:   Ricardo Dantas
* DATA:    13/04/2023
* DESCRICAO: Função que retorna as datas de movimentações da matrícula do aluno do SESI.
*********************************************************************/
RETURNS TABLE
AS
RETURN(
	SELECT
		SMATRICPL.CODCOLIGADA
		,SMATRICPL.IDPERLET
		,SMATRICPL.IDHABILITACAOFILIAL
		,SMATRICPL.RA
		,CONVERT(DATE,CASE 
			WHEN ISNULL(RECONHECIMENTO_SABERES.DATA,MATRICULA.DATA) <= STURMA.DTINICIAL THEN STURMA.DTINICIAL
			ELSE ISNULL(RECONHECIMENTO_SABERES.DATA,MATRICULA.DATA)
		END) datEfetivacaoMatricula
		,CONVERT(DATE,RECONHECIMENTO_SABERES.DATA) datEfetivacaoReconhecimento
		,CONVERT(DATE,CASE 
			WHEN SSTATUS.DESCRICAO IN ('Aprovado','APL','AEP','Concluinte','Reprovado','EPI') THEN STURMA.DTFINAL
			WHEN SSTATUS.DESCRICAO IN ('Matriculado','Inscrito','Reconhecimento de Saberes') THEN 
				CASE 
					WHEN MATRICULA.DATA <= STURMA.DTINICIAL THEN STURMA.DTINICIAL
					ELSE MATRICULA.DATA
				END
			ELSE MATRICULA.DATA
		END) datMovimentacao
		,horaReconhecimentoSaberes
	FROM SMATRICPL WITH (NOLOCK)
	LEFT JOIN STURMA WITH (NOLOCK)
		ON SMATRICPL.CODCOLIGADA = STURMA.CODCOLIGADA
		AND SMATRICPL.CODFILIAL = STURMA.CODFILIAL
		AND SMATRICPL.CODTURMA = STURMA.CODTURMA
		AND SMATRICPL.IDPERLET = STURMA.IDPERLET
	LEFT JOIN SSTATUS WITH (NOLOCK)
		ON SMATRICPL.CODCOLIGADA = SSTATUS.CODCOLIGADA
		AND SMATRICPL.CODSTATUS = SSTATUS.CODSTATUS
	LEFT JOIN (
		SELECT
			CODCOLIGADA
			,IDPERLET
			,IDHABILITACAOFILIAL
			,RA
			,MIN(DTALTERACAO) DATA
		FROM SLOGPLETIVO
		WHERE CODCOLIGADA = @CODCOLIGADA
		AND CODSTATUS IN (2,39)
		GROUP BY
			CODCOLIGADA
			,IDPERLET
			,IDHABILITACAOFILIAL
			,RA
	)MATRICULA
		ON MATRICULA.CODCOLIGADA = SMATRICPL.CODCOLIGADA
		AND MATRICULA.IDPERLET = SMATRICPL.IDPERLET
		AND MATRICULA.IDHABILITACAOFILIAL = SMATRICPL.IDHABILITACAOFILIAL
		AND MATRICULA.RA = SMATRICPL.RA
	LEFT JOIN (
		SELECT
			CODCOLIGADA
			,IDPERLET
			,IDHABILITACAOFILIAL
			,RA
			,MIN(DTALTERACAO) DATA
		FROM SLOGPLETIVO
		WHERE CODCOLIGADA = @CODCOLIGADA
		AND CODSTATUSANT = 70
		AND CODSTATUS IN (2,39)
		GROUP BY
			CODCOLIGADA
			,IDPERLET
			,IDHABILITACAOFILIAL
			,RA
	)RECONHECIMENTO_SABERES
		ON RECONHECIMENTO_SABERES.CODCOLIGADA = SMATRICPL.CODCOLIGADA
		AND RECONHECIMENTO_SABERES.IDPERLET = SMATRICPL.IDPERLET
		AND RECONHECIMENTO_SABERES.IDHABILITACAOFILIAL = SMATRICPL.IDHABILITACAOFILIAL
		AND RECONHECIMENTO_SABERES.RA = SMATRICPL.RA
	LEFT JOIN (
		SELECT
			CODCOLIGADA
			,IDPERLET
			,IDHABILITACAOFILIAL
			,RA
			,MAX(DTALTERACAO) DATA
			,35 horaReconhecimentoSaberes
		FROM SLOGPLETIVO
		WHERE CODCOLIGADA = @CODCOLIGADA
		AND CODSTATUSANT = 70
		AND CODSTATUS IN (2,39,128,129,130,131,132,42,41,20,6,45,73)
		GROUP BY
			CODCOLIGADA
			,IDPERLET
			,IDHABILITACAOFILIAL
			,RA
	)HORA_RECONHECIMENTO_SABERES
		ON HORA_RECONHECIMENTO_SABERES.CODCOLIGADA = SMATRICPL.CODCOLIGADA
		AND HORA_RECONHECIMENTO_SABERES.IDPERLET = SMATRICPL.IDPERLET
		AND HORA_RECONHECIMENTO_SABERES.IDHABILITACAOFILIAL = SMATRICPL.IDHABILITACAOFILIAL
		AND HORA_RECONHECIMENTO_SABERES.RA = SMATRICPL.RA
	WHERE 
		SMATRICPL.CODCOLIGADA = @CODCOLIGADA
	AND SMATRICPL.IDPERLET = @IDPERLET
	AND SMATRICPL.IDHABILITACAOFILIAL = @IDHABILITACAOFILIAL
	AND SMATRICPL.RA = @RA
)
GO


