USE [corporerm]
GO

/****** Object:  UserDefinedFunction [dbo].[FN_RETORNA_DATA_MOV_MAT_DISC_EB]    Script Date: 27/04/2023 19:28:23 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



CREATE FUNCTION [dbo].[FN_RETORNA_DATA_MOV_MAT_DISC_EB] (
	@CODCOLIGADA INT, 
	@IDPERLET INT,
	@IDHABILITACAOFILIAL INT,
	@IDTURMADISC INT,
	@RA VARCHAR (20)
)
/********************************************************************
* AUTOR:   Ricardo Dantas
* DATA:    14/04/2023
* DESCRICAO: Função que retorna as datas de movimentações da matrícula do aluno do SESI nas disciplinas.
*********************************************************************/
RETURNS TABLE
AS
RETURN(
	SELECT
		SMATRICPL.CODCOLIGADA
		,SMATRICPL.IDPERLET
		,SMATRICPL.IDHABILITACAOFILIAL
		,SMATRICPL.RA
		,CONVERT(DATE,MATRICULA.DATA) datEfetivacaoMatriculaDisciplina
		,CONVERT(DATE,(CASE WHEN SHABILITACAOFILIAL.CODCURSO IN ('EJA PRO','NOVA EJA') THEN MOVIMENTACAO.DATA ELSE NULL END)) datMovimentacaoDisciplina
	FROM SMATRICPL WITH (NOLOCK)
	INNER JOIN SMATRICULA WITH (NOLOCK)
		ON SMATRICULA.CODCOLIGADA = SMATRICPL.CODCOLIGADA
		AND SMATRICULA.IDPERLET = SMATRICPL.IDPERLET
		AND SMATRICULA.IDHABILITACAOFILIAL = SMATRICPL.IDHABILITACAOFILIAL
		AND SMATRICULA.RA = SMATRICPL.RA
	INNER JOIN SHABILITACAOFILIAL WITH (NOLOCK)
		ON SMATRICPL.CODCOLIGADA = SHABILITACAOFILIAL.CODCOLIGADA
		AND SMATRICPL.IDHABILITACAOFILIAL = SHABILITACAOFILIAL.IDHABILITACAOFILIAL
	INNER JOIN SSTATUS WITH (NOLOCK)
		ON SMATRICPL.CODCOLIGADA = SSTATUS.CODCOLIGADA
		AND SMATRICPL.CODSTATUS = SSTATUS.CODSTATUS
	LEFT JOIN (
		SELECT
			CODCOLIGADA
			,IDTURMADISC
			,RA
			,MIN(DTALTERACAO) DATA
		FROM SLOGMATRICULA WITH (NOLOCK)
		WHERE CODCOLIGADA = @CODCOLIGADA
		AND CODSTATUS IN (2,39)
		GROUP BY
			CODCOLIGADA
			,IDTURMADISC
			,RA
	)MATRICULA
		ON MATRICULA.CODCOLIGADA = SMATRICULA.CODCOLIGADA
		AND MATRICULA.IDTURMADISC = SMATRICULA.IDTURMADISC
		AND MATRICULA.RA = SMATRICULA.RA
	LEFT JOIN (
		SELECT
			CODCOLIGADA
			,IDTURMADISC
			,RA
			,MAX(DTALTERACAO) DATA
		FROM SLOGMATRICULA WITH (NOLOCK)
		WHERE CODCOLIGADA = @CODCOLIGADA
		GROUP BY
			CODCOLIGADA
			,IDTURMADISC
			,RA
	)MOVIMENTACAO
		ON MOVIMENTACAO.CODCOLIGADA = SMATRICULA.CODCOLIGADA
		AND MOVIMENTACAO.IDTURMADISC = SMATRICULA.IDTURMADISC
		AND MOVIMENTACAO.RA = SMATRICULA.RA
	WHERE 
		SMATRICULA.CODCOLIGADA = @CODCOLIGADA
	AND SMATRICULA.IDPERLET = @IDPERLET
	AND SMATRICULA.IDHABILITACAOFILIAL = @IDHABILITACAOFILIAL
	AND SMATRICULA.IDTURMADISC = @IDTURMADISC
	AND SMATRICULA.RA = @RA
)

GO


